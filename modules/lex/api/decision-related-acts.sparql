PREFIX lex: <http://purl.org/lex#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX sao: <http://salt.semanticauthoring.org/ontologies/sao#>
PREFIX sdo: <http://salt.semanticauthoring.org/ontologies/sdo#>

PREFIX dec: <{{"param": "resource", "filter": "RemoveLTGT"}}>
CONSTRUCT {
  ?parent dcterms:title ?actTitle .
  ?parent dcterms:identifier ?identifier.
  ?parent dcterms:hasSection ?topic
}

FROM <http://linked.opendata.cz/resource/dataset/legislation/nsoud.cz>
FROM <http://linked.opendata.cz/resource/dataset/legislation/nsoud.cz/updates>
#FROM <http://linked.opendata.cz/resource/dataset/legislation/cz/uz>
FROM <http://linked.opendata.cz/resource/dataset/legislation/psp.cz>

WHERE {
  SELECT DISTINCT ?parent ?actTitle ?identifier ?topic
  WHERE {
  dec: a lex:Decision .
  ?expression frbr:realizationOf dec:;
    sdo:hasSection/sdo:hasParagraph/sdo:hasTextChunk/sdo:hasAnnotation/sao:hasTopic ?topic .
  {
    ?topic a lex:Act .
  } UNION {
    ?topic a frbr:Work .  # pro zakony, ktere jsou jen v nsoud datasetu (stare, bez uplneho zneni)
  }
 ?topic dcterms:title ?title.
 BIND(IRI(if(contains(str(?topic), "/section/"),strbefore(str(?topic), "/section/"), str(?topic))) as ?parent) .
 ?parent dcterms:title ?actTitle.
 ?parent dcterms:identifier ?identifier.
 }
 OFFSET {{"param": "offset", "default": "0"}}
 LIMIT {{"param": "limit", "default": "10"}}
}

# Zatim se uri zakonu ziskavaji z jejich casti pomoci regexu. Az budou kompletni data (uplna zneni), nahrad regex necim vic sparql-like: napr. ?topic frbr:partOf+ ?parent. 